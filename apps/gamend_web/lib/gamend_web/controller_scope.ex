defmodule GamendWeb.ControllerScope do
  @moduledoc """
  Resolving "who is asking, and what are they in" once per request.

  `with_user/2`, `with_lobby/2` and `with_party/2` each answer the request
  themselves when the precondition fails, so a controller action reads as the
  happy path and nothing else:

      def create(conn, params) do
        with_lobby(conn, fn user, lobby -> ... end)
      end

  `with_lobby/2` and `with_party/2` were byte-identical in the chat-mute and
  ready-check controllers, which is what prompted this; `with_user/2` came
  along because they are built on it and splitting them would have left the
  same duplication one level down.
  """

  import Plug.Conn, only: [put_status: 2]

  alias Gamend.Accounts.Scope
  alias Gamend.Accounts.User
  alias Gamend.Lobbies
  alias Gamend.Parties

  @doc "Runs `fun` with the signed-in user, or replies 401."
  @spec with_user(Plug.Conn.t(), (User.t() -> Plug.Conn.t())) :: Plug.Conn.t()
  def with_user(conn, fun) do
    case Scope.user(conn.assigns[:current_scope]) do
      %User{} = user -> fun.(user)
      _ -> error(conn, :unauthorized, "Not authenticated")
    end
  end

  @doc "Runs `fun` with the user and the lobby they are in, or replies 401/400."
  @spec with_lobby(Plug.Conn.t(), (User.t(), Lobbies.Lobby.t() -> Plug.Conn.t())) :: Plug.Conn.t()
  def with_lobby(conn, fun) do
    with_user(conn, fn user ->
      if is_nil(user.lobby_id) do
        error(conn, :bad_request, "not_in_lobby")
      else
        fun.(user, Lobbies.get_lobby!(user.lobby_id))
      end
    end)
  end

  @doc "Runs `fun` with the user and the party they are in, or replies 401/400."
  @spec with_party(Plug.Conn.t(), (User.t(), Parties.Party.t() -> Plug.Conn.t())) :: Plug.Conn.t()
  def with_party(conn, fun) do
    with_user(conn, fn user ->
      case user.party_id && Parties.get_party(user.party_id) do
        %Parties.Party{} = party -> fun.(user, party)
        _ -> error(conn, :bad_request, "not_in_party")
      end
    end)
  end

  defp error(conn, status, reason) do
    conn |> put_status(status) |> Phoenix.Controller.json(%{error: reason})
  end
end
